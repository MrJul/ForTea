{
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="T4"
  psiImplClassSuffix="Impl"
  psiPackage="com.jetbrains.fortea.language.psi"
  psiImplPackage="com.jetbrains.fortea.language.psi.impl"

  elementTypeHolderClass="com.jetbrains.fortea.language.psi.T4ElementTypes"
  elementTypeClass="com.jetbrains.fortea.language.psi.T4ElementType"
  tokenTypeClass="com.jetbrains.fortea.language.psi.T4TokenType"
  psiVisitorName="T4Visitor"
  tokens=[
    CODE='regexp:foo(?=s)'
  ]
}

t4File ::= (TEXT|directive|block)*

// ---- ---- Directive ---- ----

directive ::= directive_main BLOCK_END

private directive_main ::= DIRECTIVE_START directive_name attribute* {
  pin=1
  recoverWhile=not_block_end
}

directive_name ::= TOKEN

attribute ::= attribute_name EQ QUOTE attribute_value QUOTE

attribute_name ::= TOKEN

attribute_value ::= TOKEN

// ---- ---- blocks ---- ----

block ::= code_block|expression_block|feature_block

code_block ::= CODE_BLOCK_START CODE? BLOCK_END

expression_block ::= EXPRESSION_BLOCK_START CODE? BLOCK_END

feature_block ::= FEATURE_BLOCK_START CODE? BLOCK_END

// ---- ---- Recovery ---- ----

private not_block_end ::= !BLOCK_END
