{
  parserClass="com.jetbrains.fortea.parser.T4Parser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="T4"
  psiImplClassSuffix="Impl"
  psiPackage="com.jetbrains.fortea.psi"
  psiImplPackage="com.jetbrains.fortea.psi.impl"

  elementTypeHolderClass="com.jetbrains.fortea.psi.T4ElementTypes"
  elementTypeClass="com.jetbrains.fortea.psi.T4ElementType"
  tokenTypeClass="com.jetbrains.fortea.psi.T4TokenType"
  psiVisitorName="T4Visitor"
  tokens=[
    CODE='regexp:foo(?=s)'
    BAD_CHARACTER
  ]
}

t4File ::= (TEXT|directive|block)*

// ---- ---- Directive ---- ----

directive ::= directive_main BLOCK_END

private directive_main ::= DIRECTIVE_START directive_name attribute* {
  pin=1
  recoverWhile=not_block_end_or_block_start
}

directive_name ::= TOKEN

attribute ::= attribute_name EQ QUOTE ATTRIBUTE_VALUE QUOTE

attribute_name ::= TOKEN

// ---- ---- blocks ---- ----

block ::= code_block|expression_block|feature_block

code_block ::= CODE_BLOCK_START CODE? BLOCK_END

expression_block ::= EXPRESSION_BLOCK_START CODE? BLOCK_END

feature_block ::= FEATURE_BLOCK_START CODE? BLOCK_END

// ---- ---- Recovery ---- ----

private not_block_end_or_block_start ::= !BLOCK_END
